// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    ext {
        buildToolsVersion = findProperty('android.buildToolsVersion') ?: '35.0.0'
        minSdkVersion = Integer.parseInt(findProperty('android.minSdkVersion') ?: '24')
        compileSdkVersion = Integer.parseInt(findProperty('android.compileSdkVersion') ?: '35')
        targetSdkVersion = Integer.parseInt(findProperty('android.targetSdkVersion') ?: '35')
        kotlinVersion = findProperty('android.kotlinVersion') ?: '1.9.25'

        ndkVersion = "27.1.12297006"
    }
    repositories {
        google()
        mavenCentral()
    }
    dependencies {
        classpath('com.android.tools.build:gradle')
        classpath('com.facebook.react:react-native-gradle-plugin')
        classpath('org.jetbrains.kotlin:kotlin-gradle-plugin')
    }
}

apply plugin: "com.facebook.react.rootproject"

allprojects {
    repositories {
        maven {
            // All of React Native (JS, Obj-C sources, Android binaries) is installed from npm
            url(new File(['node', '--print', "require.resolve('react-native/package.json')"].execute(null, rootDir).text.trim(), '../android'))
        }
        maven {
            // Android JSC is installed from npm
            url(new File(['node', '--print', "require.resolve('jsc-android/package.json', { paths: [require.resolve('react-native/package.json')] })"].execute(null, rootDir).text.trim(), '../dist'))
        }

        google()
        mavenCentral()
        maven { url 'https://www.jitpack.io' }
    }
}

// Ensure all CMake-based native modules are built with flexible page-size support (4KB/16KB).
subprojects { subproject ->
    def configureFlexiblePageSizes = {
        def androidExt = subproject.extensions.findByName("android")
        if (androidExt == null) {
            return
        }

        def cmake = androidExt?.defaultConfig?.externalNativeBuild?.cmake
        if (cmake == null) {
            return
        }

        def pageSizeFlag = "-DANDROID_SUPPORT_FLEXIBLE_PAGE_SIZES=ON"
        if (!cmake.arguments.contains(pageSizeFlag)) {
            cmake.arguments.add(pageSizeFlag)
        }
    }

    if (subproject.state.executed) {
        configureFlexiblePageSizes.call()
    } else {
        subproject.afterEvaluate {
            configureFlexiblePageSizes.call()
        }
    }
}
